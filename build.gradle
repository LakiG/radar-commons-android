/*
 * Copyright 2017 The Hyve
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4'
        classpath 'de.mobilej.unmock:UnMockPlugin:0.6.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

allprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'idea'

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

    version = '0.6.1-SNAPSHOT'
    ext.versionCode = 15
    group = 'org.radarcns'
    ext.githubRepoName = 'RADAR-CNS/RADAR-Commons-Android'

    ext.githubUrl = 'https://github.com/' + githubRepoName + '.git'
    ext.issueUrl = 'https://github.com/' + githubRepoName + '/issues'
    ext.website = 'http://radar-cns.org'

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                      //
//---------------------------------------------------------------------------//

    repositories {
        jcenter()
        // radar dev branches
        google()
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }

    idea {
        module {
            downloadSources = true
        }
    }

    tasks.matching { it instanceof Test }.all {
        testLogging {
            showExceptions = true
            showStandardStreams = true
            showCauses = true
            showStackTraces = true
            exceptionFormat "full"
        }
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode versionCode
        versionName version
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    libraryVariants.all { variant ->
        variant.outputs.all {
            if (variant.name == 'release') {
                outputFileName = "${archivesBaseName}-${android.defaultConfig.versionName}.aar"
            } else {
                outputFileName = "${archivesBaseName}-${android.defaultConfig.versionName}-${variant.name}.aar"
            }
        }
    }
}

ext.moduleName = 'radar-commons-android'
ext.description = 'Kafka backend for processing device data.'

dependencies {
    api('org.radarcns:radar-commons:0.7') {
        exclude group: 'io.confluent', module: 'kafka-avro-serializer'
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.json', module: 'json'
    }
    api 'org.radarcns:radar-schemas-commons:0.3.1'

    implementation 'com.google.firebase:firebase-config:11.8.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }
    implementation 'sk.baka.slf4j:slf4j-handroid:1.7.26'
}

task wrapper(type: Wrapper) {
    gradleVersion '4.4'
}

apply from: 'gradle/test.gradle'
apply from: 'gradle/publishing.gradle'
