buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 2
        versionName '0.1-alpha.2-SNAPSHOT'
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            libraryVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.aar')) {
                        output.outputFile = new File(output.outputFile.parent,
                                "${archivesBaseName}-${android.defaultConfig.versionName}.aar")
                    }
                }
            }
        }
    }
}

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

group = 'org.radarcns'
ext.moduleName = 'radar-commons-android'
version = android.defaultConfig.versionName

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                      //
//---------------------------------------------------------------------------//

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'org.apache.avro:avro:1.8.1'
    compile 'org.xerial.snappy:snappy-java:1.1.2.6'
    compile 'com.google.firebase:firebase-config:10.0.1'
    compile ('org.radarcns:radar-commons:0.2') {
        exclude group: 'io.confluent', module: 'kafka-avro-serializer'
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml'
    }
    compile 'org.radarcns:radar-schemas-commons:0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-simple:1.7.21'
    testCompile 'org.robolectric:robolectric:3.3.2'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "RADAR Commons Android"
        }
    }
    developers {
        developer {
            id "radar-cns"
            name "Joris Borgdorff"
            email "joris@thehyve.nl"
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            group project.group
            artifactId moduleName
            version project.version
        }
        pom.withXml {
            def root = asNode()
            root.appendNode('description', 'Kafka backend for processing device data.')
            root.appendNode('name', moduleName)
            root.appendNode('url', 'https://github.com/RADAR-CNS/RADAR-Commons-Android.git')
            root.children().last() + pomConfig
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = project.group
        name = moduleName
        userOrg = 'radar-cns'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/RADAR_CNS/RADAR-Commons-Android.git'
        version {
            name = project.version
            desc = 'Common Android library for the RADAR passive app'
            vcsTag = System.getenv('TRAVIS_TAG')
            released = new Date()
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
    distributionUrl distributionUrl.replace("bin", "all")
}
